buildscript {
    repositories {
        mavenCentral()
        maven {
          url "https://plugins.gradle.org/m2/"
        }
        eventuateMavenRepoUrl.split(',').each { repoUrl -> maven { url repoUrl } }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootPluginVersion")
        classpath "com.avast.gradle:gradle-docker-compose-plugin:$dockerComposePluginVersion"
        classpath "io.eventuate.plugins.gradle:eventuate-plugins-gradle-publish:$eventuatePluginsGradleVersion"
    }
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}


apply plugin: 'docker-compose'
apply plugin: "io.eventuate.plugins.gradle.publish.EventuatePublish"
apply plugin: "io.eventuate.plugins.gradle.publish.EventuatePublishDockerImages"
apply plugin: "io.eventuate.plugins.gradle.publish.PublishMultiArchContainerImages"

allprojects {
    group = "io.eventuate.common"
}

subprojects {
    java {
        if (project.name.endsWith("-spring-boot-3")) {
            toolchain {
                languageVersion.set(JavaLanguageVersion.of(17));
            }
        } else {
            toolchain {
                languageVersion.set(JavaLanguageVersion.of(17));
            }
        }
    }

    repositories {
        mavenCentral()
        eventuateMavenRepoUrl.split(',').each { repoUrl -> maven { url repoUrl } }
    }

    if (!project.name.endsWith("-bom"))
        dependencies {
            testImplementation "org.assertj:assertj-core:$assertjVersion"
            testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
            testRuntimeOnly "org.junit.platform:junit-platform-launcher:$junitPlatformLauncherVersion"
        }
}

gradle.projectsEvaluated {
    task aggregateJavaDocs(type: Javadoc) {
        description = 'Aggregates Javadoc API documentation of all subprojects.'
        group = JavaBasePlugin.DOCUMENTATION_GROUP

        destinationDir file("$buildDir/docs/javadoc")
    }
}

dockerCompose {
    removeOrphans = true
    environment.put "DOCKER_IMAGE_TAG", version
    environment.put "USE_DB_ID", useDbId
    environment.put "USE_JSON_PAYLOAD_AND_HEADERS", useJsonPayloadAndHeaders

    mysql8 {
        projectName = null
        useComposeFiles = ["docker-compose-mysql8.yml", "docker-compose-zookeeper.yml"]
        removeContainers = project.ext.removeContainers.toBoolean()
    }

    vanillamysql8 {
        projectName = null
        useComposeFiles = ["docker-compose-vanilla-mysql8-multi-arch.yml"]
        removeContainers = project.ext.removeContainers.toBoolean()
    }

    mysql8multiarch {
        projectName = null
        useComposeFiles = ["docker-compose-mysql8-multi-arch.yml", "docker-compose-zookeeper-multi-arch.yml"]
        removeContainers = project.ext.removeContainers.toBoolean()
    }

    postgresmultiarch {
        projectName = null
        useComposeFiles = ["docker-compose-postgres-multi-arch.yml", "docker-compose-zookeeper-multi-arch.yml"]
        removeContainers = project.ext.removeContainers.toBoolean()
    }

    postgres {
        projectName = null
        useComposeFiles = ["docker-compose-postgres.yml", "docker-compose-zookeeper.yml"]
        removeContainers = project.ext.removeContainers.toBoolean()
    }

    vanillapostgres {
        projectName = null
        useComposeFiles = ["docker-compose-vanilla-postgres-multi-arch.yml"]
        removeContainers = project.ext.removeContainers.toBoolean()
    }

    mssql {
        projectName = null
        useComposeFiles = ["docker-compose-mssql.yml", "docker-compose-zookeeper.yml"]
        removeContainers = project.ext.removeContainers.toBoolean()
    }

    publish {
      projectName = null
      useComposeFiles = ["docker-compose-mssql.yml"]
      removeContainers = project.ext.removeContainers.toBoolean()
    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 10, 'minutes'
    resolutionStrategy.cacheChangingModulesFor 10, 'seconds'
}

publishMultiArch {
    image "eventuateio/eventuate-mysql8"
    image "eventuateio/eventuate-postgres"
    image "eventuateio/eventuate-vanilla-mysql8"
    image "eventuateio/eventuate-zookeeper"
    image "eventuateio/eventuate-vanilla-postgres"
}

task compileAll {
    dependsOn subprojects.collect { it.tasks.withType(JavaCompile) }.flatten()
}

task preCommitCheck {
    dependsOn compileAll
    doLast {
        exec {
            commandLine 'circleci', 'config', 'validate'
        }
    }
}
tasks.withType(Test).configureEach {
    useJUnitPlatform()
}